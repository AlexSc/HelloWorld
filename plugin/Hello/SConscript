from Config import *
from Bundle import *

Import('env')
Import('libs')

files = Glob('*.cpp')

env.Append(CPPPATH=['../inc', '#/lib'], LIBPATH='#/lib')
isLinux = env['PLATFORM'] == 'posix' or env['PLATFORM'] == 'linux'
isOSX = env['PLATFORM'] == 'darwin'
isWindows = env['PLATFORM'] == 'win32'
cppdefines = []
target = 'Hello'
if isLinux or isOSX:
   cppdefines += ["XP_UNIX", 'MDCPUCFG="\\"prcpucfg_unix.h"\\"']
if isOSX:
   cppdefines += ["XP_MACOSX"]
if isWindows:
   cppdefines += ["WIN32", "_WINDOWS", 'MDCPUCFG="\\"prcpucfg_win32.h"\\"']
   files += env.RES(Glob('*.rc'))
   target = 'nphello'

env.Append(CPPDEFINES=cppdefines)
ret = env.Build('LoadableModule', target, files, LIBS_CFG=libs[1])

TOOL_BUNDLE(env)
if isOSX:
   configs = env.subst('${cfg}').split()
   for config in configs:
      config = Config.allConfigs[config]
      env.MakeBundle('Hello' + config.suffix + '.plugin', 'Hello' + config.suffix,
                     'Info.plist',
                     typecode='BRPL', creator='????',
                     resources="English.lproj")

Return('ret')

